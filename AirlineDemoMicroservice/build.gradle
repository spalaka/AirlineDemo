apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'application'

// Don't need these task, so disabling them. Makes it possible to avoid
// declaring a single application main class.
startScripts.enabled = false
run.enabled = false

// Also don't need the regular application distribution packages since
// this is just a set of samples. So disabling to make the build output
// cleaner
distTar.enabled=false
distZip.enabled=false

applicationName = 'solace-samples-jms'
version = ''

jar {
	baseName = 'solace-samples-jms'
    version =  version
    manifest {
        attributes 'Implementation-Title': 'Solace Getting Started Samples',
                   'Implementation-Version': version
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // Solace Messaging API for JMS Dependencies
    compile("com.solacesystems:sol-jms:10.5.0")
    compile("com.sun.messaging.mq:fscontext:4.6-b01")
    compile("org.json:json:20090211")
    // For any local libs that are not available from mavenCentral
    compile fileTree(dir: 'libs', include: ['*.jar'])
}

task createAllStartScripts() << {
     // just a placeholder
}

def scripts = [ 'topicPublisher':'com.solace.samples.TopicPublisher']
  scripts.each() { scriptName, className ->
        def t = tasks.create(name: scriptName+'StartScript', type: CreateStartScripts) {
        mainClassName = className
        applicationName = scriptName
        outputDir = new File(project.buildDir, 'scripts')
        classpath = jar.outputs.files + project.configurations.runtime
    }
      applicationDistribution.into("bin") {
            from(t)
            fileMode = 0755
			duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    }
    createAllStartScripts.dependsOn(t)

}

installDist {
    destinationDir = new File(project.buildDir, 'staged')
}

//To copy the data files
task manageDataFiles(type: Copy) {
   description 'Copies the resource directory to the target directory.'
   from 'src/main/resources/'
   into 'build/staged/bin/'
   include('*.txt', '**/*.xml', '**/*.properties')
   //println("description applied 12")
}

assemble.dependsOn installDist
